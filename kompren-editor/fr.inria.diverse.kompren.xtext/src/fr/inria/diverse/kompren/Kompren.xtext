grammar fr.inria.diverse.kompren.Kompren with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://kompren/fr.inria.diverse.kompren" 

Slicer returns Slicer: 'slicer' (active?='active')? (strict?='strict')? name=QName '{'
		('domain' ':' uriMetamodel+=EString)+
		'input' ':' inputClasses+=[ecore::EClass|QName] ( "," inputClasses+=[ecore::EClass|QName])*
		('radius' ':' radius=Radius)?
		('slicedClass' ':' slicedElements+=SlicedClass)*
		('slicedProperty' ':' slicedElements+=SlicedProperty)*
		('onStart' '[[' onStart=EString ']]')?
		('onEnd' '[[' onEnd=EString ']]')?
		('helper' '[[' helper=EString ']]')?
	'}';

EString returns ecore::EString: STRING | ID;

QName returns ecore::EString: EString ('.' EString)*;

EBoolean returns ecore::EBoolean: 'true' | 'false';

Constraint returns Constraint: name=EString '[[' expression=EString ']]';

Radius returns Radius: {Radius} (focusedClasses+=[SlicedClass|QName] ( "," focusedClasses+=[SlicedClass|QName])* )?;

SlicedClass returns SlicedClass: domain=[ecore::EClass|QName] (isOption?='option')? (ctx=VarDecl)? 
	('[[' expression=EString ']]')? ('constraint' ':' constraints+=Constraint)*;

VarDecl returns VarDecl: varName=ID;

SlicedProperty returns SlicedProperty:
	domain=[ecore::EStructuralFeature|QName] (isOption?='option')? (opposite=OppositeCreation)? (src=VarDecl)? (tgt=VarDecl)?
		 ('[[' expression=EString ']]')? ('constraint' ':' constraints+=Constraint)*;

OppositeCreation returns OppositeCreation: {OppositeCreation} 'opposite' '(' name=ID ')';
